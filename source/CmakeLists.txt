
find_package(Qt5 COMPONENTS Core Widgets Gui REQUIRED)
add_subdirectory(Core)
add_subdirectory(Configuration)

# JUI SETUP ###################################################

add_library(Jui SHARED
	${Core_hdr}
	${Core_src}
	${Core_res}
)

target_include_directories(Jui
	PRIVATE 
		${CMAKE_CURRENT_LIST_DIR}/Core
	INTERFACE 
		$<BUILD_INTERFACE:${CMAKE_BINARY_DIR}/include>
		$<INSTALL_INTERFACE:${PROJECT_NAME}_${PROJECT_VERSION}/include>
)

target_link_libraries(Jui
	PRIVATE
		Qt5::Core
		Qt5::Gui
		Qt5::Widgets
		#Qt5::WinMain
)

add_custom_command(TARGET Jui
	POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_BINARY_DIR}/include
		COMMAND ${CMAKE_COMMAND} -E copy_if_different ${Core_hdr} ${CMAKE_BINARY_DIR}/include
)

# CONFIG SETUP ###################################################

add_executable(Config
	${Config_hdr}
	${Config_src}
)

target_include_directories(Config 
	PRIVATE 
		${CMAKE_CURRENT_LIST_DIR}/Configuration
)
target_link_libraries(Config 
	PRIVATE 
		Qt5::Core
		Qt5::Gui
		Qt5::Widgets
		Jui
)
add_custom_command(TARGET Config
	POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_BINARY_DIR}/include
		COMMAND ${CMAKE_COMMAND} -E copy_if_different ${Config_hdr} ${CMAKE_BINARY_DIR}/include
)

string(TOUPPER ${CMAKE_CONFIGURATION_TYPES} configType)
set_target_properties( Jui Config
    PROPERTIES
		ARCHIVE_OUTPUT_DIRECTORY_${configType} ${CMAKE_BINARY_DIR}/lib
		LIBRARY_OUTPUT_DIRECTORY_${configType} ${CMAKE_BINARY_DIR}/lib
		RUNTIME_OUTPUT_DIRECTORY_${configType} ${CMAKE_BINARY_DIR}/bin
		DEBUG_POSTFIX d
		WIN32_EXECUTABLE TRUE
)

# PACKAGE BUILD SETUP ###################################################

include(CMakePackageConfigHelpers)
configure_package_config_file (
	${CMAKE_CURRENT_SOURCE_DIR}/JuiConfig.cmake.in
	${CMAKE_BINARY_DIR}/cmake/JuiConfig.cmake
	INSTALL_DESTINATION Jui_${PROJECT_VERSION}/cmake
)
write_basic_package_version_file(
	${CMAKE_BINARY_DIR}/cmake/JuiConfigVersion.cmake
	VERSION ${PROJECT_VERSION}
	COMPATIBILITY AnyNewerVersion 
)
cmake_policy(SET CMP0022 NEW)
export ( 
	TARGETS Jui Config
	FILE ${CMAKE_BINARY_DIR}/cmake/JuiTargets-${CMAKE_CONFIGURATION_TYPES}.cmake 
	EXPORT_LINK_INTERFACE_LIBRARIES
)

# PACKAGE INSTALL SETUP ###################################################

install( 
	TARGETS 
		Jui 
		Config
	EXPORT JuiTargets
	ARCHIVE DESTINATION ${PROJECT_NAME}_${PROJECT_VERSION}/lib
	LIBRARY DESTINATION ${PROJECT_NAME}_${PROJECT_VERSION}/lib
	RUNTIME DESTINATION ${PROJECT_NAME}_${PROJECT_VERSION}/bin
)
install(
	FILES 
		${Core_hdr}
		${Config_hdr}
	DESTINATION ${PROJECT_NAME}_${PROJECT_VERSION}/include
)

#[[
install(
	FILES 
		$<TARGET_FILE:Qt5::Core> 
		$<TARGET_FILE:Qt5::Gui>
		$<TARGET_FILE:Qt5::Widgets>
	DESTINATION bin
	CONFIGURATIONS Release
)
#]]


install(
	EXPORT JuiTargets
		DESTINATION ${PROJECT_NAME}_${PROJECT_VERSION}/cmake
	FILE JuiTargets.cmake
)

install(
	FILES 
		${buildDir}/cmake/JuiConfig.cmake
		${buildDir}/cmake/JuiConfigVersion.cmake
	DESTINATION	${PROJECT_NAME}_${PROJECT_VERSION}/cmake
)


